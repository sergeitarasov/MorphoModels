########################################################
#
# RevBayes Example: Morphological dating with ED model
#
# This file: Runs the full MCMC for ED model
#
# authors: Sergei Tarasov
#
# this script is build based on the global clock model for molecular data at
# https://revbayes.github.io/tutorials/dating/global
########################################################


#######################
# Reading in the Data #
#######################
analysis_name = "TA_ED-M6_exp"
Q_file = "Q_ED_M6_exp.Rev"

out_folder = "../output_" + analysis_name + "/"
chars <- readDiscreteCharacterData("../data/TA-50char.nex")
#######################


## helpers
n_taxa <- chars.size()
taxa <- chars.taxa()


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

# Load the model files

source("tree_BD.Rev") # BD tree prior

source("clock_global.Rev") # Global clock model

source(Q_file) # Morphological substitution model



########
# MCMC #
########

# initialize the model object #
mymodel = model(Q_rate)


# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename= out_folder+ analysis_name + ".log", printgen=200) )

# 2. the tree #
monitors.append( mnFile(filename= out_folder + analysis_name + ".trees", printgen=200, version=TRUE, timetree) )

# 3. the screen #
monitors.append( mnScreen(printgen=200, timetree) )


# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="mixed")

### pre-burnin to tune the proposals
mymcmc.burnin(generations=5000, tuningInterval=500)

# Run the MCMC # !!! CHANGE TO 50K
mymcmc.run(generations=200000, tuningInterval=1000)



########################
# Summarizing the trees and results #
########################

# Read the trace file #
trace = readTreeTrace(out_folder + analysis_name + ".trees")
trace.setBurnin(0.3)

# Maximum clade credibility tree #
mccTree(trace, file= out_folder + analysis_name + ".mcc.tre")

#consensus tree
consensusTree(trace, file= out_folder + analysis_name + ".mjcon.tre")

### Compute power posterior distributions
#pow_p = powerPosterior(mymodel, moves, monitors, filename="../output/posterior/" + analysis_name + ".out", cats=100)
pow_p = powerPosterior(mymodel, moves, monitors, filename= out_folder + "posterior/" + analysis_name + ".post", cats=70, sampleFreq=50)
pow_p.burnin(generations=5000, tuningInterval=500)
pow_p.run(generations=1000)

### Use stepping-stone sampling to calculate marginal likelihoods
ss = steppingStoneSampler(filename = out_folder + "posterior/" + analysis_name + ".post", powerColumnName="power", likelihoodColumnName="likelihood")
ss.marginal()


### write Marginal Ln to a file
txt_ML= analysis_name +", " + ss.marginal() + "\n"
write(txt_ML, filename="../Marginal_Ln.txt", append=TRUE)

# Quit RevBayes #
q()
